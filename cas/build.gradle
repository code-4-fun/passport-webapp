apply plugin: 'org.springframework.boot'
apply from: 'http://dl.bintray.com/scalding/generic/waroverlay.gradle'
apply from: 'https://raw.githubusercontent.com/apereo/cas/5.1.x/gradle/overrides.gradle'

apply plugin: 'eclipse'
apply plugin: 'idea'

eclipse {
    classpath {
        downloadSources = true
        downloadJavadoc = true
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}


bootRepackage {
    enabled = false
}

springBoot {
    mainClass = "org.springframework.boot.loader.WarLauncher"
}

bootRun {
    addResources = true
}

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://build.shibboleth.net/nexus/content/repositories/releases/' }
    maven { url 'https://dl.bintray.com/uniconiam/maven' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    //compile "org.apereo.cas:cas-server-webapp-tomcat:5.1.1@war"
    compile "org.apereo.cas:cas-server-webapp-tomcat:${project.'cas.version'}@war"

    // dependencies for DUO-Security MFA
    compile "org.apereo.cas:cas-server-support-duo:${project.'cas.version'}"
    compile "org.apereo.cas:cas-server-support-trusted-mfa:${project.'cas.version'}"

    // dependency for JDBC Authentication
    compile "org.apereo.cas:cas-server-support-jdbc:${project.'cas.version'}"
    compile 'mysql:mysql-connector-java:5.1.6'

    // to configure service registry
    //compile "org.apereo.cas:cas-server-support-jpa-service-registry:${project.'cas.version'}"
    compile "org.apereo.cas:cas-server-support-json-service-registry:${project.'cas.version'}"
}

task copyConfig(type: Copy) {
    def destDir = "${project.rootDir}/docker/webapp/etc/cas/config"
    println "Copying configuration to ${destDir}"
    from "${project.rootDir}/etc/cas/config"
    into "${destDir}"
}

war {
    dependsOn copyConfig
    baseName 'cas'
    includeWarJars = true
    entryCompression = ZipEntryCompression.STORED
    manifest {
        from manifestFile()
    }

    doLast {
        def sourceWarPath = project.war.outputs.files.singleFile
        def dockerWarPath = "${project.rootDir}/docker/webapp/war/"
        println "Remove existing war file if any - ${dockerWarPath}"
        delete dockerWarPath

        println "Copying ${sourceWarPath} file into ${dockerWarPath}"
        copy {
            from sourceWarPath
            into dockerWarPath
        }
    }
}

task explodeWar(type: Copy) {
    def destDir = "${buildDir}/cas"
    group = "build"
    description = "Explode the cas.war"
    from zipTree(project.war.outputs.files.singleFile)
    into destDir
    doLast {
        println "CAS web application artifact exploded into [${destDir}]"
    }
}

File manifestFile() {
    def warfile = configurations.runtime.asFileTree.matching {
        include '**/*.war'
    }
    def manifest = zipTree(warfile.singleFile).matching {
        include '**/*.MF'
    }
    def dst = new File("${project.rootDir}/etc/cas/MANIFEST.MF")
    dst.delete()
    dst << manifest.singleFile.text
    return dst
}

task generateKeys {
    group = 'CAS'
    description = 'generate keys for CAS. These keys can be added to your application.properties file'
    doLast {
        println 'Generating keys for CAS...'
        ['cas.tgc.encryptionKey': 256, 'cas.tgc.signingKey': 512, 'cas.webflow.encryption.key': 96, 'cas.webflow.signing.key': 512].each { key, size ->
            def octetKey = OctJwkGenerator.generateJwk(size)
            def params = octetKey.toParams(JsonWebKey.OutputControlLevel.INCLUDE_SYMMETRIC)
            println "${key}=${params.get('k')}"
        }
    }
}

task run {
    dependsOn build
    doLast {
        javaexec {
            main = "-jar";
            jvmArgs = [
                    "-Xmx2048M"
            ]
            args = [
                    "build/libs/cas.war"
            ]
        }
    }
}

task debug {
    dependsOn build
    doLast {
        javaexec {
            main = "-jar";
            jvmArgs = [
                    "-Xmx2048M",
                    "-XX:+TieredCompilation",
                    "-XX:TieredStopAtLevel=1",
                    "-Xdebug",
                    "-Xrunjdwp:transport=dt_socket,address=5005,server=y,suspend=y"
            ]
            args = [
                    "build/libs/cas.war",
            ]

            println "Started ${commandLine}"
        }
    }
}
